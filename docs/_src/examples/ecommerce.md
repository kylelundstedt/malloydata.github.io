# eCommerce Walkthrough: An Intro to Malloy Syntax & the VSCode Plugin
_The following walk-through covers similar concepts to the [Quick Start](https://github.com/looker-open-source/malloy/#quick-start-videos) videos in the README. You can find the complete source code for this model [here](https://github.com/looker-open-source/malloy/blob/docs-release/samples/ecommerce/ecommerce.malloy)._

Malloy queries compile to SQL. As Malloy queries become more complex, the SQL complexity expands dramatically, while the Malloy query remains concise and easier to read.

A couple of key concepts: Queries in Malloy start with a data source, specified either `explore some_named_explore` or `explore 'some_table'`, followed by piped transformations on that data. Fields used in queries may be named using the `is` keyword, which is similar to `AS` in SQL, but reversed.

Let’s illustrate this by asking a straightforward question of a simple ecommerce dataset - how many order items have we sold, broken down by their current status?

```malloy
--! {"isRunnable": true, "source": "ecommerce/ecommerce.malloy", "size": "large"}
explore 'malloy-data.ecomm.order_items' | reduce
 status
 order_item_count is count(*)
```

The `reduce` transformation in the above query invokes a `SELECT` with a `GROUP BY` in SQL. Malloy also has a `project` transformation, which will `SELECT` without a `GROUP BY`.

Notice that after you write this, a small “Run” code lens will appear above the query. Click this to run the query. This will produce the following SQL:

```sql
SELECT
  base.status as status,
  COUNT( 1) as order_item_count
FROM malloy-data.ecomm.order_items as base
GROUP BY 1
ORDER BY 2 desc
```

_Note: To see the SQL being generated by your query, open up a New Terminal in the top menu, then select Output, and pick “Malloy” from the menu on the right._

![Kapture 2021-08-18 at 17 07 03](https://user-images.githubusercontent.com/7178946/130125702-7049299a-fe0f-4f50-aaed-1c9016835da7.gif)


Next question: In 2020, how much did we sell to users in each state? This requires filtering to the year 2020, excluding cancelled and returned orders, as well as joining in the users table.
```malloy
--! {"isRunnable": true, "source": "ecommerce/ecommerce.malloy", "size": "large"}
explore 'malloy-data.ecomm.order_items'
  users is join ('malloy-data.ecomm.users' primary key id) on user_id
| reduce : [created_at: @2020, status != 'Cancelled' & != 'Returned']
 users.state
 total_sales is sale_price.sum()
```

Note that queries can be filtered at any level, by inserting filter expressions between square brackets. A filter after an explore applies to the whole explore; one before the fields in a `reduce` or `project` transformation applies to that transformation; and one after an aggregate field applies to that aggregate only. See filters documentation for more information on filter expressions. Here's an example with a variety of filter usage:

```malloy
--! {"isRunnable": true, "source": "ecommerce/ecommerce.malloy", "size": "large"}
explore order_items : [users.state: 'California' | 'New York' | 'Texas', status: != 'Cancelled' & != 'Processing']
| reduce
  users.state
  total_sale_price_2020 is sale_price.sum() : [created_at : @2020]
  percent_items_returned is 100.0 * (count() : [status : 'Returned']) / count()
```

At this point we might notice we’re defining a few things we might like to re-use, so let’s add them to the model:
```malloy
define users is (explore 'malloy-data.ecomm.users'
  primary key id
);

define order_items is (explore 'malloy-data.ecomm.order_items'
 primary key id
 total_sales is sale_price.sum()
 users is join on user_id);
```

Having defined this in the model, the VSCode plugin will give us handy "Outline" and "Schema" tools in the left menu. "Outline" provides an interactive navigator of the model, in order, and "Schema" shows all of the attributes (both raw fields in the underlying table, and the dimensions, measures, and named queries you've defined in your model).

<img width="281" alt="Screen Shot 2021-08-27 at 4 21 15 PM" src="https://user-images.githubusercontent.com/7178946/131198480-28410cb0-9482-40b4-bc6f-5d67ecdeda2a.png">


Our query is now very simple:
```malloy
--! {"isRunnable": true, "source": "ecommerce/ecommerce.malloy", "size": "large"}
explore order_items | reduce : [created_at: @2020]
 users.state
 total_sales
```

At this point, the VSCode plugin will also provide us with a handy

To further simplify, we can add this and a couple other queries we’ll frequently use to our model. Once you define these, the VSCode plugin will supply a “Run” button next to each query:
```malloy
sales_by_state_2020 is (reduce: [created_at: @2020]
   users.state
   total_sales
 )

 orders_by_status is (reduce
   status
   order_count is count(distinct order_id)
 )

 sales_by_month_2020 is (reduce : [created_at : @2020]
   order_month is created_at.month
   total_sales
 )
```

Allowing us to run the following very simple command next time we want to run any of these queries:
```malloy
--! {"isRunnable": true, "source": "ecommerce/ecommerce.malloy", "size": "large"}
explore order_items | sales_by_state_2020
```


Queries can contain other nested structures, by including additional transformations as fields, so our named query (`sales_by_month_2020`) can also now be called anywhere as a nested structure. Note that these structures can nest infinitely!:

```malloy
--! {"isRunnable": true, "source": "ecommerce/ecommerce.malloy", "size": "large"}
explore order_items | reduce
 users.state
 total_sales
 orders_by_status
 sales_by_month_2020
```

Which can be visualized using a data_style
```
{"sales_by_month_2020" : {
   "renderer" : "line_chart"}
, "orders_by_status" : {
   "renderer" : "bar_chart"}
}
```
<img width="899" alt="Screen Shot 2021-08-18 at 4 44 01 PM" src="https://user-images.githubusercontent.com/7178946/130128542-e122eab9-5cf2-48cb-b87c-ce520517d595.png">


Putting a few named queries together as nested structures allows us to produce a dashboard with an overview of sales, having written remarkably little code. Use the `dashboard` renderer to format the results like this:

```malloy
state_dashboard is (reduce
 users.state
 total_sales
 order_count
 orders_by_status
 sales_by_month_2020
)
```

<img width="865" alt="Screen Shot 2021-08-19 at 12 01 56 PM" src="https://user-images.githubusercontent.com/7178946/130128823-6b7f8e97-ec28-4ced-9e38-c72384eb976b.png">
