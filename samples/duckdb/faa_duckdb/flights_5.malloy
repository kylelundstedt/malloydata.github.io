import "flights.malloy"

// Aircraft analysis
//  

source: flights_aircraft is flights {
  measure:
    total_distance is sum(distance)
    seats_for_sale is sum(aircraft.aircraft_models.seats)
    seats_owned is aircraft.sum(aircraft.aircraft_models.seats)
    average_plane_size is aircraft.avg(aircraft.aircraft_models.seats)
    average_flight_distance is distance.avg()

  query: carrier_aircraft is {
    group_by: carriers.nickname
    aggregate: 
      aircraft.aircraft_count
      average_plane_size
      flight_count
      average_flight_distance
      seats_owned
    nest: by_manufacturer is {
      group_by: aircraft.aircraft_models.manufacturer
      aggregate:
        aircraft.aircraft_count
        average_plane_size
        flight_count
        average_flight_distance
        seats_owned
    }
  }
}